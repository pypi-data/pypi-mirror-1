# -*- coding: utf-8 -*-
##----------------------------------------------------------------------
## Common rules
## Root of rulebase. Other rulebase files are extending common
##----------------------------------------------------------------------
## Copyright (C) 2007-2009 The NOC Project
## See LICENSE for details
##----------------------------------------------------------------------

##
## e1 and e2 are different events belong to same managed object
##
same_object
    use same_object($e1, $e2)
    when
        fm.managed_object($e1, $o)
        fm.managed_object($e2, $o)
        check $e1 != $e2
##
## e1 and e2 are belong to same event class
##
same_class
    use same_class($e1, $e2)
    when
        fm.event_class($e1, $ec)
        fm.event_class($e2, $ec)
##
## e1 and e2 occured in tw seconds
##
in_time
    use in_time($e1, $e2, $tw)
    when
        fm.timestamp($e1, $t1)
        fm.timestamp($e2, $t2)
        check abs($t1-$t2)<$tw
##
## e1 occured after e2
##
follow
    use follow($e1, $e2)
    when
        fm.timestamp($e1,$t1)
        fm.timestamp($e2,$t2)
        check $t1>=$t2 and $e1>$e2
##
## e matches the head of list of event classes
##
classes_1
    use classes($e, ($c, *$rest))
    when
        fm.event_class($e,$c)
##
## e matches the rest of list of event classes
##
classes_2
    use classes($e, ($_, *$rest))
    when
        classes($e, $rest)
##
## No more vars list to match
##
same_vars_done
    use same_vars($e1, $e2, ())
##
## match head of list of vars and the rest
##
same_vars_1
    use same_vars($e1, $e2, ($v1, *$rest))
    when
        fm.var($e1, $v1, $x)
        fm.var($e2, $v1, $x)
        same_vars($e1, $e2, $rest)
##
## There is event between two given events, of given class
## with given vars
##
between_object_classes_vars
    use between_object_classes_vars($e1, $e2, $C, $V)
    when
        follow($e1, $e3)
        follow($e3, $e2)
        same_object($e1, $e3)
        same_vars($e1, $e3, $V)
        classes($e3, $C)
##
## Match pair of events:
## 1. e1 follows e2
## 2. e1, e2 matches list of classes C
## 3. e1, e2 have common values of variables V
## 4. There is no other event between e1 and e2 with properties 2, 3
##
seq_object_classes_vars
    use seq_object_classes_vars($e1, $e2, $C, $V)
    when
        same_object($e1, $e2)
        follow($e1, $e2)
        same_vars($e1, $e2, $V)
        classes($e1, $C)
        classes($e2, $C)
        notany
            between_object_classes_vars($e1, $e2, $C, $V)
