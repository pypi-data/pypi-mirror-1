######################################################################
##### Setup stuff ####################################################
######################################################################
AC_PREREQ(2.53)

AC_INIT(pygtkimageview, 1.0.0, [http://trac.bjourne.webfactional.com])

# foreign makes it so I don't need stupid NEWS, INSTALL, etc files.
AM_INIT_AUTOMAKE([foreign])

######################################################################
##### Check for different programs ###################################
######################################################################
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

PKG_CHECK_MODULES(PYGTK, pygtk-2.0 >= 2.8.0)
AC_SUBST(PYGTK_CFLAGS)
AC_SUBST(PYGTK_LIBS)

PKG_CHECK_MODULES(GTKIMAGEVIEW, gtkimageview >= 1.4.0)
AC_SUBST(GTKIMAGEVIEW_CFLAGS)
AC_SUBST(GTKIMAGEVIEW_LIBS)

# Check for Python
AM_PATH_PYTHON(2.2)
AM_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR(could not find Python headers or library)])

AC_PATH_PROG(PYGTK_CODEGEN, pygtk-codegen-2.0, no)
AC_PATH_PROG(GLIB_MKENUMS, glib-mkenums, no)

AC_MSG_CHECKING(for pygtk defs)
PYGTK_DEFSDIR=`$PKG_CONFIG --variable=defsdir pygtk-2.0`
AC_SUBST(PYGTK_DEFSDIR)
AC_MSG_RESULT($PYGTK_DEFSDIR)

######################################################################
##### Twiddle CFLAGS #################################################
######################################################################
CFLAGS="${CFLAGS} -Wall -Werror -std=c99 -Wno-missing-prototypes"

######################################################################
##### Output files ###################################################
######################################################################
AC_CONFIG_FILES([
Makefile
docs/Makefile
src/Makefile
tests/Makefile
])

AC_OUTPUT

echo "

Configuration:

        == Global settings ==
        Compiler:           ${CC}
        CFLAGS:             ${CFLAGS}

        == PyGTK settings ==
        PYGTK_CFLAGS        ${PYGTK_CFLAGS}
        PYGTK_CODEGEN:      ${PYGTK_CODEGEN}
        PYGTK_DEFSDIR:      ${PYGTK_DEFSDIR}

        == Python settings ==
        pythondir:          ${pythondir}
        pkgpythondir:       ${pkgpythondir}

        == glib settings ==
        GLIB_MKENUMS:       ${GLIB_MKENUMS}

        == GtkImageView settings ==
        GTKIMAGEVIEW_CFLAGS ${GTKIMAGEVIEW_CFLAGS}
"
